
; == 08_wait_cancel.ks =====================================

; [wait_cancel]の資料。

; ==========================================================



; [set_default_view]
; これは独自マクロです(→macro.ks)。
; 解説画面を作成します。
[set_default_view]



;-----------------------------------------------------------
*Part1
;-----------------------------------------------------------

#akane
（１）普通の選択肢[p]

; [glink]×３
; グラフィカルリンクを３つ出します。
[glink target="*Part2" text="選択肢１" y="110" color="blue" size="24" x="220" width="440"]
[glink target="*Part2" text="選択肢２" y="190" color="blue" size="24" x="220" width="440"]
[glink target="*Part2" text="選択肢３" y="270" color="blue" size="24" x="220" width="440"]

; [s]
; ゲームを停止。
; このように、通常は、[glink]を設置した後は
; 必ず[s]でゲームを停止させなければいけませんが……。
[s]



;-----------------------------------------------------------
*Part2
;-----------------------------------------------------------

#akane
（２）２秒経つとゲームオーバーする選択肢[p]

; [glink]×３
; グラフィカルリンクを３つ出します。
[glink target="*Part3" text="選択肢１" y="110" color="black" size="24" x="220" width="440"]
[glink target="*Part3" text="選択肢２" y="190" color="black" size="24" x="220" width="440"]
[glink target="*Part3" text="選択肢３" y="270" color="black" size="24" x="220" width="440"]

; [wait]
; ここで[s]の代わりに[wait]を用います!!!
[wait time="2000"]

; [jump]
; この行は、選択肢を表示した状態で2000ミリ秒待った場合に読まれます。
; この行を読んだ瞬間*Part2.1に飛ばします。
[jump target="Part2.1"]

;-----------------------------------------------------------
*Part2.1
;-----------------------------------------------------------

; このラベルはゲームオーバーラベルです。

; [cm]
; フリーレイヤーに存在するグラフィカルリンクを解放します。
; グラフィカルリンクをクリックしてジャンプした場合は自動で解放されるのですが、
; 単に[jump]タグで飛んできた場合は解放されませんので、手動で解放せねばなりません。
[cm]

#akane
-GAME OVER-[p]

; [jump]
; もとの場所に戻ります。
[jump target="*Part2"]



;-----------------------------------------------------------
*Part3
;-----------------------------------------------------------

; [wait_cancel]
; [wait]中に飛んでくる可能性がある場所には、
; 必ず[wait_cancel]を配置して、wait命令をキャンセルしましょう。
; 配置しないと、テキストが勝手に進む恐れがあります。
; (この場合で言えば、「2000ミリ秒後に次のスクリプトを読み込め」
; という命令が生きている可能性があるため。きちんと殺しておきましょう）
[wait_cancel]

#akane
（３）３秒経つと選択肢が増える選択肢[p]

; [glink]×３
; グラフィカルリンクを３つ出します。
[glink target="*Part4" text="選択肢１" y="110" color="white" size="24" x="220" width="440"]
[glink target="*Part4" text="選択肢２" y="190" color="white" size="24" x="220" width="440"]
[glink target="*Part4" text="選択肢３" y="270" color="white" size="24" x="220" width="440"]

; [wait]
; ここで[s]の代わりに[wait]を用います!!!
[wait time="3000"]

; [glink]
; 新たな選択肢を提示します。
[glink target="*Part4" text="選択肢４" y="350" color="r e d" size="24" x="220" width="440"]

; [s]
; 選択肢を表示した後は停止が基本です。
[s]


;-----------------------------------------------------------
*Part4
;-----------------------------------------------------------

; [wait_cancel]
; [wait]中に飛んでくる可能性がありますので、[wait_cancel]でキャンセル。
[wait_cancel]

#akane
以上です[p]


[jump storage="select.ks"]